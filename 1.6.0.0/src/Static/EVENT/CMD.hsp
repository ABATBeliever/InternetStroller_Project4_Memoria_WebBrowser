//ユーザ操作に起因するイベントの静的定義
*CMD_BACK					
	gosub *TAB_GETINFO
	gosub *EVENT_URL
	if ( pTabInfo ){ ret = CCall2(dpCtrl(1), WV2_get_GoBack, 0) }
	return

*CMD_FORWARD				
	gosub *TAB_GETINFO
	gosub *EVENT_URL
	if ( pTabInfo ){ ret = CCall2(dpCtrl(1), WV2_get_GoForward, 0) }
	return

*CMD_RELOAD					
	gosub *TAB_GETINFO
	gosub *EVENT_URL
	if ( pTabInfo ){ ret = CCall2(dpCtrl(1), WV2_Reload, 0) }
	return

*CMD_NAVIGATE
	tmp="false"
	gosub *TAB_GETINFO
	gosub *EVENT_URL
	title bufTmp
	if instr(bufTmp,0,"https://")=0{tmp="true"}		//これらに含まれるならサイトとして
	if instr(bufTmp,0,"http://")=0{tmp="true"}		//でなければ検索
	if instr(bufTmp,0,"edge://")=0{tmp="true"}
	if instr(bufTmp,0,"file://")=0{tmp="true"}
	if instr(bufTmp,0,"mailto://")=0{tmp="true"}
	if instr(bufTmp,0,"ftp://")=0{bufTmp="https://"+bufTmp:tmp="true"}
	if instr(bufTmp,0,"ftps://")=0{bufTmp="https://"+bufTmp:tmp="true"}
	if instr(bufTmp,0,"sftp://")=0{bufTmp="https://"+bufTmp:tmp="true"}
	if instr(bufTmp,0,"view-source:")=0{bufTmp="https://"+bufTmp:tmp="true"}
	if VARIABLE_CONF_AUTOSEARCH!=0{tmp="true"}
	if tmp="true"{
		GetWindowTextW hEditUrl, varptr(bufTmp), p1:ret = CCall2(dpCtrl(1), WV2_Navigate, 1, varptr(bufTmp))
	}else{
		wvurl = VARIABLE_CONF_URL_SEARCH+bufTmp:gosub *CMD_WV_RUN
	}
	return
	
*CMD_NEW_TAB
	gosub *TAB_ADD
	return

*CMD_CLONE_TAB
	gosub *EVENT_URL
	if ( pTabInfo == 0 ){ return }
	p1 = (GetWindowTextLengthW(hEditUrl) + 1) * 2
	sdim bufTmp, p1
	GetWindowText hEditUrl, varptr(bufTmp), p1
	wvurl = bufTmp
	gosub *CMD_WV_RUN
	return

*CMD_DEL_TAB
	gosub *TAB_DEL
	return

*CMD_ABOUT
	gosub *EVENT_URL
	screen 4, 500, 300, 4
	IShwndBit(4)=1
	width ,,(ginfo(0)-300),(ginfo(1)-300)
	gosub *SetColor
	if VARIABLE_CONF_MDI=0{GetWindowLong hWnd,-16:SetWindowLong hWnd,-16,stat|$00CF0000:SetParent hWnd,hClient:gsel 4:width ,,0,0}
	gsel 4,2
	title "About"
	GetWindowLong hwnd, -16
	SetWindowLong hwnd, -16, stat - $20000
	about_text="ABATBeliever "+ISNAME+" "+ISVersion+"\nBuild : "+ISBuildNumber+" ("+ISBuildDay+")\nHSPバージョン:"+__hspver__
	about_text+="\n\nCopyright (C) 2021-2024 ABATBeliever.\nhttps://abatbeliever.net/InternetStroller/\n\n"
	about_text+={"
InternetStrollerMemoria(ISMemoria)はWebViewを利用したHSP製ブラウザです。
MicrosoftEdgeと比べてUIや、ブラウジング以外の機能が単純なため極めて軽量です。
USBメモリなどに入れて「インターネットを散歩」しましょう！
(Strollerはベビーカーを指す名詞ですが、命名当時は中学生だったので知りませんでした。Stroll+er)

ブラウジング性能はWebView2のためEdge水準で、RSSや自作スクリプト、PDF書き出しなどの機能があります。
逆に、パスワードマネージャーやVPNなどはありません。
全ての設定と履歴は同ディレクトリに生成されます。
高DPIやMDIなどいろいろサポートしているので、ゲーミングPCから小型タブレットまで。

<各種リンク>
開発リポジトリ
https://github.com/ABATBeliever/InternetStroller_Project4_Memoria_WebBrowser
HSP3でWebViewを動作させるコード・ブラウザのサンプル
https://blog.goo.ne.jp/hiro239415/e/5041aec497e02427c2b123aef07977e3
WebView2Loader.dll
https://www.nuget.org/packages/Microsoft.Web.WebView2
トースト通知(クラッシュ時)
https://hsp.tv/play/pforum.php?mode=pastwch&num=47647
UserAgent変更
https://hsp.tv/play/pforum.php?mode=all&num=100931
iniモジュール(許諾の元改造)
https://dev.onionsoft.net/seed/info.ax?id=2367
複数選択肢ダイアログ
https://hsp.moe/#taskdialog
複数ファイル対応型dialog
https://hsp.tv/play/pforum.php?mode=all&num=101616
高DPI 
https://learn.microsoft.com/ja-jp/windows/win32/api/winuser/nf-winuser-setprocessdpiaware?redirectedfrom=MSDN
HotSoupProcessor3(3.6)
https://hsp.tv/

HSPブラウザの存在を知るきっかけとなった、Krone(Look Explorer)並びにYotiosoft氏、
自作ブラウザ界隈と、多くの個人製作ブラウザ(Monot,Flast,Flune等)に心から感謝。"}
	mesbox about_text, 500, 300, 0, 0				//KnoreはWalls,HSPのOSもどきより Monot/Flast/FluneはElectron(2023年頃Twitterより)
	return
	
*CMD_ABOUT_2
	gsel 4,-1
	return

*CMD_UPDATE
	check=getData(VARIABLE_CONF_URL_UPDATECHECK)+"=[EOF]"	//起動時に自動で確認する
	if instr(check,0,"Ver")!=0{								//IsUpdateCheckは、二回目以降(自発的に選択)は負のレポートも
		if IsUpdateCheck=1{dialog "更新確認に失敗しました\nUpdate check failed"}
		return
	}
	split check,"=",tmp,check
	if instr(check,0,str(ISCommonVer))!=0{
		dialog "最新版がリリースされています\nNew versions available\n\n===========================\n"+ISCommonVer+"=>"+check+"==========================="//バージョンが同じでない
		wvurl = VARIABLE_CONF_URL_BROWSERUPDATE
		gosub *CMD_WV_RUN
	}else:if IsUpdateCheck=1{dialog "最新です\nYour Version is Latest."}//自発的に調べたなら
	return
	
*CMD_CAPTURE
	gosub *EVENT_URL
	modTaskSelect=modTasks("Internet Stroller Memoria - Capture","Which format do you prefer?\nどっちの形式にしますか?","PNG/PDF/Cancel","PNG\nPDF\nCancel")
		if modTaskSelect="1" {gosub *CMD_CAPTURE1}
		if modTaskSelect="2" {gosub *CMD_CAPTURE2}
		if modTaskSelect="3" {return}
		if modTaskSelect="0" {return}
		if modTaskSelect="-1" {return}
	return

*CMD_CAPTURE1
	dialog "png", 17
	if ( stat == 0 ){ return }
	if ( SHCreateStreamOnFileEx(refstr, 0x1002, 0x80, 1, 0, p1) ){ return }
	gosub *TAB_GETINFO
	if ( pTabInfo == 0 ){ return }
	dupptr dp, WV2_DATA_SIZE * 20 + pTabInfo, WV2_DATA_SIZE * 4, 4
	WebView2_EventInit WV2_EVENT_CAPTURE, hWnd0, 0, dp
	ret = CCall2(dpCtrl(1), WV2_CapturePreview, 3, 0, p1, dp)
	WebView2_EventWait dp
	WebView2_Release p1
	dialog "Captured PNG\n>>"+refstr
	return

*CMD_CAPTURE2
	dialog "pdf", 17
	if ( stat == 0 ){ return }
	gosub *TAB_GETINFO
	if ( pTabInfo == 0 ){ return }
	bPDF = 1
	mi(1) = 1 : mi(3) = 3
	dupptr dp, WV2_DATA_SIZE * 24 + pTabInfo, WV2_DATA_SIZE * 4, 4
	WebView2_EventInit WV2_EVENT_PRINTTOPDF, hWnd0, 0x9005, dp
	sdim bufTmp, (strlen(refstr) + 1) * 2
	cnvstow bufTmp, refstr
	ret = CCall2(dpCtrl(1), WV2_PrintToPdf, 3, varptr(bufTmp), 0, dp)
	dialog "Captured PDF\n>>"+refstr
	return

*CMD_INICONF
	dialog "File : "+dirinfo(1)+"\\ISConfig.ini\n\nVersion "+VARIABLE_CONF_INIFILEVER+"\nWriter : "+VARIABLE_CONF_INIFILEWRITER+"\nLanguage : "+VARIABLE_CONF_INIFILELANGUAGE
	return
	
*CMD_DOWNLOAD
	gosub *TAB_GETINFO
	if ( pTabInfo == 0 ){ return }
	if ( CCall2(dpCtrl(1), WV2_IsDownloadDialogOpen, 1, varptr(p1)) ){ return }
	if ( p1 ){ p1 = WV2_CloseDownloadDialog } else { p1 = WV2_OpenDownloadDialog }
	ret = CCall2(dpCtrl(1), p1, 0)
	return

*CMD_LOCAL
	gosub *TAB_GETINFO
	gosub *EVENT_URL
	gosub *TAB_UPDATE
	dialog16Multi res
	if stat = 0 {return}	//独自の複数ファイル選択のdialog
	if stat = 1 {cnvstoa res,res:wvurl = "file://"+res:gosub *CMD_WV_RUN:return}
	tmp=""
	repeat (length(res)-1)
		tmp=tmp+res(0)+"\\"+res(cnt+1)+"\n"
	loop
	dialog ""+str(length(res)-1)+" elements have been loaded.\nDo you want to load?\n\n"+str(length(res)-1)+"の要素が読み込まれました\n読み込みますか?\n\n"+tmp,2
	if stat=7{return}
	repeat (length(res)-1)
		wvurl = "file://"+res(0)+"\\"+res(cnt+1)
		cnvstoa wvurl,str(wvurl)
		gosub *CMD_WV_RUN
	loop
	return

*CMD_HISTORY
	wvurl = "edge://history"
	gosub *CMD_WV_RUN
	stop

*CMD_FUNC
	screen 5,300,280,4
	IShwndBit(5)=1
	width ,,(ginfo(0)-30),(ginfo(1)-50)
	if VARIABLE_CONF_MDI=0{GetWindowLong hWnd,-16:SetWindowLong hWnd,-16,stat|$00CF0000:SetParent hWnd,hClient:gsel 5:width ,,0,0}
	gsel 5,2
	gosub *SetColor
	GetWindowLong hwnd, -16
	SetWindowLong hwnd, -16, stat - $20000
	title VARIABLE_CONF_TEXT_FUNCTION_NAME
	objsize 300,40
	pos 0,0:button gosub VARIABLE_CONF_TEXT_UPDATE, *CMD_UPDATE
	pos 0,40:button gosub VARIABLE_CONF_TEXT_INICONFIG, *CMD_INICONF
	pos 0,80:button gosub "Professional Features", *CMD_FUNC_ADVANCED
	hButton = objinfo(stat, 2)
	pos 0,120:button gosub VARIABLE_CONF_TEXT_ISABOUT, *CMD_ABOUT
	pos 0,240:button gosub VARIABLE_CONF_TEXT_CLOSE, *CMD_FUNC_CLOSE
	gsel 0
	return

*CMD_FUNC_CLOSE	
	gsel 5,-1
	return

*CMD_FUNC_ADVANCED
	GetWindowRect hButton, varptr(RECT)	//選択時にイベント
	TrackPopupMenu hMenu.1, $100, RECT.2, RECT.1, 0, hwnd, 0
	if stat = 0 : stop
	if stat = $100 : ShellExecuteW 0, "open", "notepad", dirinfo(1)+"\\ISConfig.ini", 0, 10
	
	if stat = $200 : gosub *CMD_FUNC_CONFDEL
	if stat = $210 : gosub *CMD_FUNC_INDEXDEL
	
	if stat = $300 : gosub *CMD_FUNC_TASKMGR
	if stat = $310 : gosub *CMD_FUNC_EXECUTE
	
	if stat = $400 : wvurl="edge://about" : gosub *CMD_WV_RUN
	if stat = $410 : wvurl="https://github.com/ABATBeliever/InternetStroller_Project4_Memoria_WebBrowser" : gosub *CMD_WV_RUN
	if stat = $420 : wvurl="https://blog.goo.ne.jp/hiro239415/e/5041aec497e02427c2b123aef07977e3" : gosub *CMD_WV_RUN
	if stat = $430 : wvurl="https://www.nuget.org/packages/Microsoft.Web.WebView2" : gosub *CMD_WV_RUN
	if stat = $440 : wvurl="https://hsp.tv/play/pforum.php?mode=pastwch&num=47647" : gosub *CMD_WV_RUN
	if stat = $450 : wvurl="https://hsp.tv/play/pforum.php?mode=all&num=100931" : gosub *CMD_WV_RUN
	if stat = $460 : wvurl="https://dev.onionsoft.net/seed/info.ax?id=2367" : gosub *CMD_WV_RUN
	if stat = $470 : wvurl="https://hsp.moe/#taskdialog" : gosub *CMD_WV_RUN
	if stat = $480 : wvurl="https://hsp.tv/play/pforum.php?mode=all&num=101616" : gosub *CMD_WV_RUN
	if stat = $490 : wvurl="https://learn.microsoft.com/ja-jp/windows/win32/api/winuser/nf-winuser-setprocessdpiaware" : gosub *CMD_WV_RUN

	if stat = $500 : wvurl=VARIABLE_CONF_URL_REPORT : gosub *CMD_WV_RUN
	if stat = $510 : SetProcessDPIAware : dialog "done."

	if stat = $600 : gosub *IScript

	if stat = $700 : wvurl="edge://bluetooth-internals/" : gosub *CMD_WV_RUN
	if stat = $701 : wvurl="edge://browser-essentials/" : gosub *CMD_WV_RUN
	if stat = $702 : wvurl="edge://components/" : gosub *CMD_WV_RUN
	if stat = $703 : wvurl="edge://credits/" : gosub *CMD_WV_RUN
	if stat = $704 : wvurl="edge://download-internals/" : gosub *CMD_WV_RUN
	if stat = $705 : wvurl="edge://net-internals/#dns" : gosub *CMD_WV_RUN
	if stat = $706 : wvurl="edge://flags/" : gosub *CMD_WV_RUN
	if stat = $707 : wvurl="edge://gpu/" : gosub *CMD_WV_RUN
	if stat = $708 : wvurl="edge://network-errors/" : gosub *CMD_WV_RUN
	if stat = $709 : wvurl="edge://profile-internals/" : gosub *CMD_WV_RUN
	if stat = $710 : wvurl="edge://quota-internals/" : gosub *CMD_WV_RUN
	if stat = $711 : wvurl="edge://sandbox/" : gosub *CMD_WV_RUN
	if stat = $712 : wvurl="edge://site-engagement/" : gosub *CMD_WV_RUN
	if stat = $713 : wvurl="edge://sync-internals/" : gosub *CMD_WV_RUN
	if stat = $714 : wvurl="edge://system/" : gosub *CMD_WV_RUN
	if stat = $715 : wvurl="edge://terms/" : gosub *CMD_WV_RUN
	if stat = $716 : wvurl="edge://tracing/" : gosub *CMD_WV_RUN
	if stat = $717 : wvurl="edge://ukm/" : gosub *CMD_WV_RUN
	if stat = $718 : wvurl="edge://usb-internals/" : gosub *CMD_WV_RUN
	if stat = $719 : wvurl="edge://webrtc-internals/" : gosub *CMD_WV_RUN
	if stat = $720 : wvurl="edge://webrtc-logs/" : gosub *CMD_WV_RUN
	
	if stat = $900 : gosub *ISWindowEXIT
	gsel 0
	stop

*CMD_FUNC_CONFDEL	//Confの再生成
	dialog "ISConfig.ini will deleted.\nCreate configuration from scratch.\nWhat language do you want?\nEnglish(Y)/Japanese(N)\n\nISConfig.iniの設定を新しく作ります。\n言語はどうしますか? 英語(Y)/日本語(N)", 3, "conf.ini Select - English/Japanese"
	conf_lang=stat
 	if conf_lang="6" {bcopy "iE.pak",""+dir_cur+"\\ISConfig.ini"}
 	if conf_lang="7" {bcopy "iJ.pak",""+dir_cur+"\\ISConfig.ini"}
 	gosub *loadini
 	title "Loaded(New Setting)"
	return

*CMD_FUNC_INDEXDEL
	bcopy "html.pak",""+dir_cur+"\\index.html"
 	dialog "SUCSESS"
	return
	
*CMD_WV_RUN
	gsel 0
	NOW_DEF_URL = DEF_URL
	DEF_URL = wvurl
	gosub *TAB_ADD
	DEF_URL = NOW_DEF_URL
	return

*POP_URL	//便利ボタン
	if IsUpdateCheck!=1{return}
	GetWindowRect hButton1, varptr(RECT1)
	TrackPopupMenu hMenu.2, $100, RECT1.2, RECT1.1, 0, hwnd, 0
	if stat = 0 : return
	if stat = $800 : gosub *CMD_TRANSLATE
	if stat = $810 : gosub *CMD_HISTORY
	if stat = $820 : gosub *CMD_LOCAL
	if stat = $830 : gosub *CMD_SEARCH
	if stat = $840 : gosub *CMD_CLONE_TAB
	if stat = $860 : gosub *Browser_PUSH_Chrome
	if stat = $870 : gosub *Browser_PUSH_Edge
	if stat = $880 : gosub *Browser_PUSH_Firefox
	if stat = $890 : gosub *IScript
return

*CMD_TRANSLATE
	if ( pTabInfo == 0 ){ return }
	p1 = (GetWindowTextLengthW(hEditUrl) + 1) * 2
	sdim bufTmp, p1
	GetWindowText hEditUrl, varptr(bufTmp), p1
	wvurl = VARIABLE_CONF_URL_TRANSLATE+bufTmp
	gosub *CMD_WV_RUN
	return

*CMD_SEARCH
	if ( pTabInfo == 0 ){ return }
	p1 = (GetWindowTextLengthW(hEditUrl) + 1) * 2
	sdim bufTmp, p1
	GetWindowText hEditUrl, varptr(bufTmp), p1
	wvurl = VARIABLE_CONF_URL_SEARCH+bufTmp
	gosub *CMD_WV_RUN
	return

*CMD_FUNC_TASKMGR
	gosub *TAB_GETINFO
	if ( pTabInfo == 0 ){ return }
	ret = CCall2(dpCtrl(1), WV2_OpenTaskManagerWindow, 0)
	return

*DefinePopup
	CreatePopupMenu
	hmenu.4 = stat		//各種項目
		AppendMenu hMenu.4, 0, $700, "BlueTooth Config"
		AppendMenu hMenu.4, 0, $701, "Browser Essentials"
		AppendMenu hMenu.4, 0, $702, "Components"
		AppendMenu hMenu.4, 0, $703, "Credits"
		AppendMenu hMenu.4, 0, $704, "Download Internals"
		AppendMenu hMenu.4, 0, $705, "DNS LookUp (and Events/Proxy/Sockets/DSP/SDCH)"
		AppendMenu hMenu.4, 0, $706, "Flags [Experimental WebView Features]"
		AppendMenu hMenu.4, 0, $707, "GPU"
		AppendMenu hMenu.4, 0, $708, "NetWork Errs [HTTP ERROR Code List]"
		AppendMenu hMenu.4, 0, $709, "Profile"
		AppendMenu hMenu.4, 0, $710, "Quota [Disk / Bucket Usage}"
		AppendMenu hMenu.4, 0, $711, "Sand Box"
		AppendMenu hMenu.4, 0, $712, "Site Engagement [Your favorite Sites List]"
		AppendMenu hMenu.4, 0, $713, "Sync"
		AppendMenu hMenu.4, 0, $714, "System Version Data"
		AppendMenu hMenu.4, 0, $715, "Microsoft WebView / MSEdge License"
		AppendMenu hMenu.4, 0, $716, "Traching [BenchMark]"
		AppendMenu hMenu.4, 0, $717, "UKM Debug Page"
		AppendMenu hMenu.4, 0, $718, "USB Internals"
		AppendMenu hMenu.4, 0, $719, "Web RTC Internals [Create]"
		AppendMenu hMenu.4, 0, $720, "Web RTC Logs"
	CreatePopupMenu
	hMenu.3 = stat
		AppendMenu hMenu.3, 0, $420, "WebView Control (from goo)"
		AppendMenu hMenu.3, 0, $430, "WebView2Loader (from Nuget)"
		AppendMenu hMenu.3, 0, $440, "Toast Notification (from hsp.tv)"
		AppendMenu hMenu.3, 0, $450, "User Agent Change (from hsp.tv)"
		AppendMenu hMenu.3, 0, $460, "ini Module (from hsp.tv)"
		AppendMenu hMenu.3, 0, $470, "MultiSelect Dialog (from hsp.moe)"
		AppendMenu hMenu.3, 0, $480, "MultiFile Dialog (from hsp.tv)"
		AppendMenu hMenu.3, 0, $490, "DPI Support (from hsp.tv)"
	CreatePopupMenu
	hMenu.2 = stat
		AppendMenu hMenu.2, 0, $800, VARIABLE_CONF_TEXT_TRANSLATE+"..."
		AppendMenu hMenu.2, 0, $810, VARIABLE_CONF_TEXT_HISTORY+"..."
		AppendMenu hMenu.2, 0, $820, VARIABLE_CONF_TEXT_LOCAL+"..."
		AppendMenu hMenu.2, $800, 0, ""
		AppendMenu hMenu.2, 0, $830, VARIABLE_CONF_TEXT_SEARCH+"..."
		AppendMenu hMenu.2, 0, $840, VARIABLE_CONF_TEXT_CLONETAB+"..."
		AppendMenu hMenu.2, $800, 0, ""
		AppendMenu hMenu.2, 0, $860, "Send URL to Chrome/Chromium"
		AppendMenu hMenu.2, 0, $870, "Send URL to Edge"
		AppendMenu hMenu.2, 0, $880, "Send URL to Firefox"
		AppendMenu hMenu.2, $800, 0, ""
		AppendMenu hMenu.2, 0, $890, "IScript GUI for Memoria"
	CreatePopupMenu
	hMenu.1 = stat
		AppendMenu hMenu.1, 0, $100, "Edit Config"
		AppendMenu hMenu.1, $800, 0, ""
		AppendMenu hMenu.1, 0, $200, "Reset ISConfig"
		AppendMenu hMenu.1, 0, $210, "Reset index.html"
		AppendMenu hMenu.1, $800, 0, ""
		AppendMenu hMenu.1, 0, $300, "WebView Taskmgr..."
		AppendMenu hMenu.1, 0, $310, "JavaScript Execute..."
		AppendMenu hMenu.1, $10, hMenu.4, "Other Menu..."
		AppendMenu hMenu.1, $800, 0, ""
		AppendMenu hMenu.1, 0, $400, "WebView2 Internal Components List"
		AppendMenu hMenu.1, 0, $410, "Open Internet Stroller's Github Repository"
		AppendMenu hMenu.1, $10, hMenu.3, "Sourse Library"
		AppendMenu hMenu.1, $800, 0, ""
		AppendMenu hMenu.1, 0, $500, "Report Issue..."
		AppendMenu hMenu.1, 0, $510, "DPI Re-Declare"
		AppendMenu hMenu.1, $800, 0, ""
		AppendMenu hMenu.1, 0, $600, "IScript GUI for Memoria"
		AppendMenu hMenu.1, $800, 0, ""
		AppendMenu hMenu.1, 0, $900, "Close Browser"
return