//ユーザ操作に起因するイベントの静的定義
*CMD_BACK					
	gosub *TAB_GETINFO
	gosub *EVENT_URL
	if ( pTabInfo ){ ret = CCall2(dpCtrl(1), WV2_get_GoBack, 0) }
	return

*CMD_FORWARD				
	gosub *TAB_GETINFO
	gosub *EVENT_URL
	if ( pTabInfo ){ ret = CCall2(dpCtrl(1), WV2_get_GoForward, 0) }
	return

*CMD_RELOAD					
	gosub *TAB_GETINFO
	gosub *EVENT_URL
	if ( pTabInfo ){ ret = CCall2(dpCtrl(1), WV2_Reload, 0) }
	return

*CMD_NAVIGATE
	tmp="false"
	gosub *TAB_GETINFO
	gosub *EVENT_URL
	title bufTmp
	if instr(bufTmp,0,"https://")=0{tmp="true"}		//これらに含まれるならサイトとして
	if instr(bufTmp,0,"http://")=0{tmp="true"}		//でなければ検索
	if instr(bufTmp,0,"edge://")=0{tmp="true"}
	if instr(bufTmp,0,"file://")=0{tmp="true"}
	if instr(bufTmp,0,"mailto://")=0{tmp="true"}
	if instr(bufTmp,0,"ftp://")=0{bufTmp="https://"+bufTmp:tmp="true"}
	if instr(bufTmp,0,"ftps://")=0{bufTmp="https://"+bufTmp:tmp="true"}
	if instr(bufTmp,0,"sftp://")=0{bufTmp="https://"+bufTmp:tmp="true"}
	if instr(bufTmp,0,"view-source:")=0{bufTmp="https://"+bufTmp:tmp="true"}
	if getSini("IsEnableAUTOSEARCH")!=0{tmp="true"}
	if tmp="true"{
		GetWindowTextW hEditUrl, varptr(bufTmp), p1:ret = CCall2(dpCtrl(1), WV2_Navigate, 1, varptr(bufTmp))
	}else{
		wvurl = getSini("IsSearchURL")+bufTmp:gosub *CMD_WV_RUN
	}
	return
	
*CMD_NEW_TAB
	gosub *TAB_ADD
	return

*CMD_CLONE_TAB
	gosub *EVENT_URL
	if ( pTabInfo == 0 ){ return }
	p1 = (GetWindowTextLengthW(hEditUrl) + 1) * 2
	sdim bufTmp, p1
	GetWindowText hEditUrl, varptr(bufTmp), p1
	wvurl = bufTmp
	gosub *CMD_WV_RUN
	return

*CMD_DEL_TAB
	gosub *TAB_DEL
	return

*CMD_ABOUT
	gosub *EVENT_URL
	screen 4, 500, 300, 4
	IShwndBit(4)=1
	width ,,(ginfo(0)-300),(ginfo(1)-300)
	gosub *SetColor
	if getSini("IsEnableMDI")=0{GetWindowLong hWnd,-16:SetWindowLong hWnd,-16,stat|$00CF0000:SetParent hWnd,hClient:gsel 4:width ,,0,0}
	gsel 4,2
	title "About"
	GetWindowLong hwnd, -16
	SetWindowLong hwnd, -16, stat - $20000
	about_text="ABATBeliever "+ISNAME+" "+ISVersion+"\nBuild : "+ISBuildNumber+" ("+ISBuildDay+")\nHSPバージョン:"+__hspver__
	about_text+="\n\nCopyright (C) 2021-2025 ABATBeliever.\nhttps://abatbeliever.net/InternetStroller/\n\n"
	about_text+={"
InternetStrollerMemoria(ISMemoria)はWebViewを利用したHSP製ブラウザです。
MicrosoftEdgeと比べてUIや、ブラウジング以外の機能が単純なため極めて軽量です。
USBメモリなどに入れて「インターネットを散歩」しましょう！
(Strollerはベビーカーを指す名詞ですが、命名当時は中学生だったので知りませんでした。Stroll+er)

ブラウジング性能はWebView2のためEdge水準で、RSSや自作スクリプト、PDF書き出しなどの機能があります。
逆に、パスワードマネージャーやVPNなどはありません。
全ての設定と履歴は同ディレクトリに生成されます。
高DPIやMDIなどいろいろサポートしているので、ゲーミングPCから小型タブレットまで。

<各種リンク>
開発リポジトリ
https://github.com/ABATBeliever/InternetStroller_Project4_Memoria_WebBrowser
HSP3でWebViewを動作させるコード・ブラウザのサンプル
https://blog.goo.ne.jp/hiro239415/e/5041aec497e02427c2b123aef07977e3
WebView2Loader.dll
https://www.nuget.org/packages/Microsoft.Web.WebView2
トースト通知(クラッシュ時)
https://hsp.tv/play/pforum.php?mode=pastwch&num=47647
UserAgent変更
https://hsp.tv/play/pforum.php?mode=all&num=100931
iniモジュール(許諾の元改造)
https://dev.onionsoft.net/seed/info.ax?id=2367
複数選択肢ダイアログ
https://hsp.moe/#taskdialog
複数ファイル対応型dialog
https://hsp.tv/play/pforum.php?mode=all&num=101616
高DPI 
https://learn.microsoft.com/ja-jp/windows/win32/api/winuser/nf-winuser-setprocessdpiaware?redirectedfrom=MSDN
HotSoupProcessor3(3.6)
https://hsp.tv/

HSPブラウザの存在を知るきっかけとなった、Krone(Look Explorer)並びにYotiosoft氏、
自作ブラウザ界隈と、多くの個人製作ブラウザ(Monot,Flast,Flune等)に心から感謝。"}
	about_text="サンプル"
	mesbox about_text, 500, 300, 0, 0				//KnoreはWalls,HSPのOSもどきより Monot/Flast/FluneはElectron(2023年頃Twitterより)
	sdim about_text
	return
	
*CMD_ABOUT_2
	gsel 4,-1
	return

*CMD_UPDATE
	check=getData(getSini("IsCheckUpdateURL"))+"=[EOF]"	//起動時に自動で確認する
	if instr(check,0,"Ver")!=0{								//IsUpdateCheckは、二回目以降(自発的に選択)は負のレポートも
		if IsUpdateCheck=1{dialog "更新確認に失敗しました\nUpdate check failed"}
		return
	}
	split check,"=",tmp,check
	if instr(check,0,str(ISCommonVer))!=0{
		dialog "最新版がリリースされています\nNew versions available\n\n===========================\n"+ISCommonVer+"=>"+check+"==========================="//バージョンが同じでない
		wvurl = getSini("IsGETUpdateURL")
		gosub *CMD_WV_RUN
	}else:if IsUpdateCheck=1{dialog "最新です\nYour Version is Latest."}//自発的に調べたなら
	return
	
*CMD_CAPTURE
	gosub *EVENT_URL
	modTaskSelect=modTasks("Internet Stroller Memoria - Capture","Which format do you prefer?\nどっちの形式にしますか?","PNG/PDF/Cancel","PNG\nPDF\nCancel")
		if modTaskSelect="1" {gosub *CMD_CAPTURE1}
		if modTaskSelect="2" {gosub *CMD_CAPTURE2}
		if modTaskSelect="3" {return}
		if modTaskSelect="0" {return}
		if modTaskSelect="-1" {return}
	return

*CMD_CAPTURE1
	dialog "png", 17
	if ( stat == 0 ){ return }
	if ( SHCreateStreamOnFileEx(refstr, 0x1002, 0x80, 1, 0, p1) ){ return }
	gosub *TAB_GETINFO
	if ( pTabInfo == 0 ){ return }
	dupptr dp, WV2_DATA_SIZE * 20 + pTabInfo, WV2_DATA_SIZE * 4, 4
	WebView2_EventInit WV2_EVENT_CAPTURE, hWnd0, 0, dp
	ret = CCall2(dpCtrl(1), WV2_CapturePreview, 3, 0, p1, dp)
	WebView2_EventWait dp
	WebView2_Release p1
	dialog "Captured PNG\n>>"+refstr
	return

*CMD_CAPTURE2
	dialog "pdf", 17
	if ( stat == 0 ){ return }
	gosub *TAB_GETINFO
	if ( pTabInfo == 0 ){ return }
	bPDF = 1
	mi(1) = 1 : mi(3) = 3
	dupptr dp, WV2_DATA_SIZE * 24 + pTabInfo, WV2_DATA_SIZE * 4, 4
	WebView2_EventInit WV2_EVENT_PRINTTOPDF, hWnd0, 0x9005, dp
	sdim bufTmp, (strlen(refstr) + 1) * 2
	cnvstow bufTmp, refstr
	ret = CCall2(dpCtrl(1), WV2_PrintToPdf, 3, varptr(bufTmp), 0, dp)
	dialog "Captured PDF\n>>"+refstr
	return

*CMD_INICONF
	dialog "廃止"
	return
	
*CMD_DOWNLOAD
	gosub *TAB_GETINFO
	if ( pTabInfo == 0 ){ return }
	if ( CCall2(dpCtrl(1), WV2_IsDownloadDialogOpen, 1, varptr(p1)) ){ return }
	if ( p1 ){ p1 = WV2_CloseDownloadDialog } else { p1 = WV2_OpenDownloadDialog }
	ret = CCall2(dpCtrl(1), p1, 0)
	return

*CMD_LOCAL
	gosub *TAB_GETINFO
	gosub *EVENT_URL
	gosub *TAB_UPDATE
	dialog16Multi res
	if stat = 0 {return}	//独自の複数ファイル選択のdialog
	if stat = 1 {cnvstoa res,res:wvurl = "file://"+res:gosub *CMD_WV_RUN:return}
	tmp=""
	repeat (length(res)-1)
		tmp=tmp+res(0)+"\\"+res(cnt+1)+"\n"
	loop
	dialog ""+str(length(res)-1)+" elements have been loaded.\nDo you want to load?\n\n"+str(length(res)-1)+"の要素が読み込まれました\n読み込みますか?\n\n"+tmp,2
	if stat=7{return}
	repeat (length(res)-1)
		wvurl = "file://"+res(0)+"\\"+res(cnt+1)
		cnvstoa wvurl,str(wvurl)
		gosub *CMD_WV_RUN
	loop
	return

*CMD_HISTORY
	wvurl = "edge://history"
	gosub *CMD_WV_RUN
	stop

*CMD_Extend
	screen 5,300,280,4
	IShwndBit(5)=1
	width ,,(ginfo(0)-30),(ginfo(1)-50)
	if getSini("IsEnableMDI")=0{GetWindowLong hWnd,-16:SetWindowLong hWnd,-16,stat|$00CF0000:SetParent hWnd,hClient:gsel 5:width ,,0,0}
	gsel 5,2
	gosub *SetColor
	GetWindowLong hwnd, -16
	SetWindowLong hwnd, -16, stat - $20000
	title "廃止予定"
	objsize 300,40
	pos 0,0:button gosub getSini("IsTextUpdate"), *CMD_UPDATE
	pos 0,80:button gosub "Professional Features", *CMD_FUNC_ADVANCED
	hButton = objinfo(stat, 2)
	pos 0,120:button gosub "About", *CMD_ABOUT
	pos 0,240:button gosub getSini("IsTextClose"), *CMD_FUNC_CLOSE
	gsel 0
	return

*CMD_FUNC_CLOSE	
	gsel 5,-1
	return

*CMD_FUNC_ADVANCED
*CMD_Extend_ADVANCED
	GetWindowRect hButton, varptr(RECT)	//選択時にイベント
	TrackPopupMenu hMenu.1, $100, RECT.2, RECT.1, 0, hwnd, 0
	gosub *CMD_POPUPStat
	return

*CMD_FUNC_CONFDEL	//Confの再生成
	dialog "ISConfig.ini will deleted.\nCreate configuration from scratch.\nWhat language do you want?\nEnglish(Y)/Japanese(N)\n\nISConfig.iniの設定を新しく作ります。\n言語はどうしますか? 英語(Y)/日本語(N)", 3, "conf.ini Select - English/Japanese"
	conf_lang=stat
 	if conf_lang="6" {bcopy "iE.pak",""+dir_cur+"\\ISConfig.ini"}
 	if conf_lang="7" {bcopy "iJ.pak",""+dir_cur+"\\ISConfig.ini"}
// 	gosub *loadini
 	title "Loaded(New Setting)"
	return

*CMD_FUNC_INDEXDEL
	bcopy "html.pak",""+dir_cur+"\\index.html"
 	dialog "SUCSESS"
	return
	
*CMD_WV_RUN
	gsel 0
	NOW_DEF_URL = DEF_URL
	DEF_URL = wvurl
	gosub *TAB_ADD
	DEF_URL = NOW_DEF_URL
	return

*CMD_START	//便利ボタン
	GetWindowRect hSTARTButton, varptr(RECT1)
	TrackPopupMenu hMenu.2000, $100, RECT1.2, RECT1.1, 0, hwnd, 0
	gosub *CMD_POPUPStat
	return

*CMD_POPUPStat
	if stat = 0 : return	//Cancel
	
	//2000 便利ボタン系
	if stat = $2001 : gosub *CMD_TRANSLATE
	if stat = $2002 : gosub *CMD_HISTORY
	if stat = $2003 : gosub *CMD_LOCAL
	if stat = $2004 : gosub *CMD_SEARCH
	if stat = $2005 : gosub *CMD_CLONE_TAB
	if stat = $2006 : gosub *Browser_PUSH_Chrome
	if stat = $2007 : gosub *Browser_PUSH_Edge
	if stat = $2008 : gosub *Browser_PUSH_Firefox
	if stat = $2009 : gosub *IScript

	//3000 拡張系
	if stat = $3001 : ShellExecuteW 0, "open", "notepad", dirinfo(1)+"\\ISConfig.ini", 0, 10
	if stat = $3002 : gosub *CMD_FUNC_CONFDEL
	if stat = $3003 : gosub *CMD_FUNC_INDEXDEL
	if stat = $3004 : gosub *CMD_FUNC_TASKMGR
	if stat = $3005 : gosub *CMD_FUNC_EXECUTE
	if stat = $3006 : wvurl="edge://about" : gosub *CMD_WV_RUN
	if stat = $3007 : wvurl="https://github.com/ABATBeliever/InternetStroller_Project4_Memoria_WebBrowser" : gosub *CMD_WV_RUN
	if stat = $3008 : wvurl=getSini("IsReportURL") : gosub *CMD_WV_RUN
	if stat = $3009 : SetProcessDPIAware : dialog "done."
	if stat = $3010 : gosub *IScript
	if stat = $3011 : gosub *ISWindowEXIT

	//3100 拡張系
	if stat = $3101 : wvurl="edge://bluetooth-internals/" : gosub *CMD_WV_RUN
	if stat = $3102 : wvurl="edge://browser-essentials/" : gosub *CMD_WV_RUN
	if stat = $3103 : wvurl="edge://components/" : gosub *CMD_WV_RUN
	if stat = $3104 : wvurl="edge://credits/" : gosub *CMD_WV_RUN
	if stat = $3105 : wvurl="edge://download-internals/" : gosub *CMD_WV_RUN
	if stat = $3106 : wvurl="edge://net-internals/#dns" : gosub *CMD_WV_RUN
	if stat = $3107 : wvurl="edge://flags/" : gosub *CMD_WV_RUN
	if stat = $3108 : wvurl="edge://gpu/" : gosub *CMD_WV_RUN
	if stat = $3109 : wvurl="edge://network-errors/" : gosub *CMD_WV_RUN
	if stat = $3110 : wvurl="edge://profile-internals/" : gosub *CMD_WV_RUN
	if stat = $3111 : wvurl="edge://quota-internals/" : gosub *CMD_WV_RUN
	if stat = $3112 : wvurl="edge://sandbox/" : gosub *CMD_WV_RUN
	if stat = $3113 : wvurl="edge://site-engagement/" : gosub *CMD_WV_RUN
	if stat = $3114: wvurl="edge://sync-internals/" : gosub *CMD_WV_RUN
	if stat = $3115: wvurl="edge://system/" : gosub *CMD_WV_RUN
	if stat = $3116 : wvurl="edge://terms/" : gosub *CMD_WV_RUN
	if stat = $3117 : wvurl="edge://tracing/" : gosub *CMD_WV_RUN
	if stat = $3118 : wvurl="edge://ukm/" : gosub *CMD_WV_RUN
	if stat = $3119 : wvurl="edge://usb-internals/" : gosub *CMD_WV_RUN
	if stat = $3120 : wvurl="edge://webrtc-internals/" : gosub *CMD_WV_RUN
	if stat = $3121 : wvurl="edge://webrtc-logs/" : gosub *CMD_WV_RUN
	
	//3200 拡張系
	if stat = $3201 : wvurl="https://blog.goo.ne.jp/hiro239415/e/5041aec497e02427c2b123aef07977e3" : gosub *CMD_WV_RUN
	if stat = $3202 : wvurl="https://www.nuget.org/packages/Microsoft.Web.WebView2" : gosub *CMD_WV_RUN
	if stat = $3203 : wvurl="https://hsp.tv/play/pforum.php?mode=pastwch&num=47647" : gosub *CMD_WV_RUN
	if stat = $3204 : wvurl="https://hsp.tv/play/pforum.php?mode=all&num=100931" : gosub *CMD_WV_RUN
	if stat = $3205 : wvurl="https://dev.onionsoft.net/seed/info.ax?id=2367" : gosub *CMD_WV_RUN
	if stat = $3206 : wvurl="https://hsp.moe/#taskdialog" : gosub *CMD_WV_RUN
	if stat = $3207 : wvurl="https://hsp.tv/play/pforum.php?mode=all&num=101616" : gosub *CMD_WV_RUN
	if stat = $3208 : wvurl="https://learn.microsoft.com/ja-jp/windows/win32/api/winuser/nf-winuser-setprocessdpiaware" : gosub *CMD_WV_RUN

	return

*CMD_TRANSLATE
	if ( pTabInfo == 0 ){ return }
	p1 = (GetWindowTextLengthW(hEditUrl) + 1) * 2
	sdim bufTmp, p1
	GetWindowText hEditUrl, varptr(bufTmp), p1
	wvurl = getSini("IsTranslateURL")+bufTmp
	gosub *CMD_WV_RUN
	return

*CMD_SEARCH
	if ( pTabInfo == 0 ){ return }
	p1 = (GetWindowTextLengthW(hEditUrl) + 1) * 2
	sdim bufTmp, p1
	GetWindowText hEditUrl, varptr(bufTmp), p1
	wvurl = getSini("IsSearchURL")+bufTmp
	gosub *CMD_WV_RUN
	return

*CMD_FUNC_TASKMGR
	gosub *TAB_GETINFO
	if ( pTabInfo == 0 ){ return }
	ret = CCall2(dpCtrl(1), WV2_OpenTaskManagerWindow, 0)
	return

*CMD_SYSCLICK
	if wparam=IsClickSystemMenu1{
		TrackPopupMenu hMenu.1000, $100, ginfo(0), ginfo(1), 0, hwnd, 0
		gosub *CMD_POPUPStat
		}
	return