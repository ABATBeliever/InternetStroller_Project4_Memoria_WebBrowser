#include "hsp3utf.as"			//UTF-8

#packopt version "Build.ini"	//詳細
#packopt icon "image.ico"		//アイコン
#epack "mswv.pak"				//WebView2Loader.dll
#epack "hsc.pak"				//HSPinet.dll
#epack "html.pak"				//既定html
#epack "iE.pak"					//英語版ユーザデータ
#epack "iJ.pak"					//日本語版ユーザデータ

/*
	□---------------------------□
	 | Internet Stroller Memoria |
	□---------------------------□
	Copyright(C) ABATBeliever 2021-2024
	Website:"https:/abatbeliever.net/InternetStroller/"

	Permission is hereby granted, free of charge, to any person obtaining a copy
	of this software and associated documentation files (the "Software"), to deal
	in the Software without restriction, including without limitation the rights
	to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
	copies of the Software, and to permit persons to whom the Software is
	furnished to do so, subject to the following conditions:
	
	The above copyright notice and this permission notice shall be included in all
	copies or substantial portions of the Software.
	
	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
	IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
	FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
	AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
	LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
	OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
	SOFTWARE.
*/

//===============================================================================
#define ISName "InternetStroller Memoria"
#define ISDeepVersion "Beta"
#define ISCommonVer "3160"
#define ISBuildNumber "3.160.896.0"
#define ISBuildDay ""+__date__+" "+__time__
ISVersion=ISDeepVersion+" Ver 1.6.0.0C"

screen 0,1000,640,,(ginfo(20)/2-500),(ginfo(21)/2-320) : hBrowser=hwnd : title ISName
redraw 0

//IShwndOpenは1で開いている判定
repeat 7
	IShwndOpen(cnt)=0
loop

//===============================================================================
#include "shell32.as"					//0 (メインウィンドウ)
										//1 RSS
#include "src/modTaskDialog.hsp"		//2 IScript
#include "src/ini_system.hsp"			//3 ManaPAD
#include "src/MultiDialog.hsp"			//4 Aboutウィンドウ
#include "src/hspinet.as"				//5 5 FunctionManager
#include "src/Notification_Manager.hsp"	//6 (JavaScript実行)

#uselib "gdi32.dll"
#uselib "user32.dll"
#func SetProcessDPIAware "SetProcessDPIAware"	//DPIパッチの準備
#include "src/WindowColor.hsp"					//ウィンドウカラーの変更定義

//===============================================================================
#module
	#deffunc GetInit
		ini_load "ISConfig.ini"					//iniローダーの初期化
	return

	#deffunc Set str GetiniSection					//セクション指定
		ini_section_sel GetiniSection
	return

	#defcfunc Get str GetiniStr	//iniロード
		sdim tmp , 1024
		ini_get GetiniStr , tmp
		if stat=1 {
			dialog "Invalid value (does not exist or cannot be read)\nNew settings may have been added as a result of a browser update.\nMissing value '"+GetiniStr+"'\nEdit(Y)/Ignore(N)\n\n値が無効です\nブラウザが更新された結果、新しい設定が追加された可能性があります\n存在しない値 '"+GetiniStr+"["+GetiniSection+"]'\n編集(Y)/無視(N)",3,"ISConfig Error"
			if stat=6{ShellExecuteW 0, "open", "notepad", dirinfo(1)+"\\ISConfig.ini", 0, 10:end}
		}
	return tmp

	#defcfunc FileCheck str filename
		exist filename
		if strsize=-1{return 1}
	return 0
#global

//===============================================================================
*INIConf
ini_set						//設定データの準備・ロードのUI
ini_load "ISConfig.ini"
	if stat{
		modTaskSelect=modTasks("Internet Stroller Memoria - No Config","There is no configuration file.\n有効な設定ファイルがありません","Please select the language for the ISConfig.ini.\nISConfig.iniを作るので言語を選択してください。","English\n日本語\nRetry\nExit")
			if modTaskSelect="1" {bcopy "iE.pak",""+dir_cur+"\\ISConfig.ini":dialog "It is set to high DPI mode.\nIf problems arise, you can disable it by setting \"REG_DPI\" to 1 in ini.":goto *ContinueLoad}
			if modTaskSelect="2" {bcopy "iJ.pak",""+dir_cur+"\\ISConfig.ini":dialog "高DPIモードにこの後設定されます。問題が生じた場合はiniの「REG_DPI」を1にすると無効化できます。":goto *ContinueLoad}
			if modTaskSelect="3" {goto *INIConf}
			if modTaskSelect="4" {end}
			goto *INIConf
	}

//===============================================================================
*ContinueLoad
GetInit
if VARIABLE_CONF_DPI=0{SetProcessDPIAware:screen 0,1000,600:title ISName}//DPIパッチ
font "FixedSys"

if FileCheck("WebView2Loader.dll")=1{bcopy "mswv.pak",""+dir_cur+"\\WebView2Loader.dll"}

#include "src/LoadWebView.hsp"				//WebViewロード
#include "src/SetEvent.hsp"					//WebView系イベント登録
gosub *Loadini								//データ取得
gosub *SetColor

#include "src/Graphic.hsp"					//UI初期描画

//===============================================================================
create_statusbar ""	//ステータスバー描画初期化
p=240,270,-1:sendmsg h_stbar,$404,3,varptr(p)
tmp=ISName+" "+ISVersion:set_statusbar 0,tmp//ISName

if dir_cmdline = "" {                    //初期URL。引数指定があればそれを、無ければiniより
	DEF_URL = VARIABLE_CONF_URL_FIRST
	if VARIABLE_CONF_URL_FIRST = "include" { //内蔵が指定された場合はindex.htmlを展開
		exist dir_cur + "\\index.html"
		if strsize = -1 {bcopy "html.pak",""+dir_cur+"\\index.html"+VARIABLE_CONF_PAGECOLOR}
		DEF_URL="file://"+dir_cur+"\\index.html"+VARIABLE_CONF_PAGECOLOR
	}
	gosub *TAB_ADD
}else{
	DEF_URL = dir_cmdline
	gosub *TAB_ADD
	DEF_URL="file://"+dir_cur+"\\index.html"
}
	
if VARIABLE_CONF_MDI=0{hClient=hWnd:GetWindowLong hClient,-16 :SetWindowLong hClient,-16,stat|$00CF0000}//MDIが有効な場合親に登録

gosub *TAB_UPDATE
gosub *WM_SIZE
gosub *DefinePopup
onerror *ERROR

if FileCheck(""+dir_exe+"\\hspinet.dll")=1{bcopy "hsc.pak",""+dir_exe+"\\hspinet.dll"}
HSPSelfConnect_init	//RSSフィード向けの通信初期化

redraw 1//コミット
stop

//===============================================================================
#include "src/HSC.hsp"			//独自通信コンポーネント
#include "src/StatusBar32.hsp"	//ステータスバーの定義

#include "src/WindowEvent.hsp"	//ウィンドウ系イベントの番地
#include "src/TabEvent.hsp"		//タブ系のイベント
#include "src/WebViewEvent.hsp"	//WebViewのイベント
#include "src/UserEvent.hsp"	//ユーザ起点のイベント

#include "src/RSSer.hsp"		//RSSフィード
#include "src/ManaPAD.hsp"		//タブレット向け操作パネル
#include "src/Execute6.hsp"		//JavaScript実行
#include "src/IScript.hsp"		//IScriptインタプリタ
#include "src/URLPush.hsp"		//URL移動

//===============================================================================
*ERROR	//エラー時にトースト通知を出して終了
SetTrayIconFile "user32.dll"
CreateTrayIcon "",1,0
PopupBalloonTip "Error Report ["+ISVersion+"]",""+ISName+" has Self-Stopped.\n\nError Type>>"+wparam+"\nPlease Report to ABATBeliever",2,0
end

//===============================================================================
*Loadini	//iniにロード開始
Set "HEAD"
VARIABLE_CONF_INIFILEWRITER=Get("WRITER")
VARIABLE_CONF_INIFILEVER=int(Get("VERSION"))
if int(VARIABLE_CONF_INIFILEVER) != 3 {dialog "ISConfig.ini is unknown Version or can't load.\nISConfig.iniが古いバージョンです。削除してください。":end}
VARIABLE_CONF_INIFILELANGUAGE=Get("LANGUAGE")
VARIABLE_CONF_INIFILEBUILD=Get("WRITEDAY")

Set "REG"
VARIABLE_CONF_ISCRIPTENABLE=int(Get("ISCRIPTENABLE"))
VARIABLE_CONF_MDI=int(Get("MDI"))
VARIABLE_CONF_DPI=int(Get("DPI")):if VARIABLE_CONF_DPI=0{DPISETPOS=1}else{DPISETPOS=0}
VARIABLE_CONF_UA=Get("DEFUA"):if VARIABLE_CONF_UA!=""{SetEnvironmentVariable "WEBVIEW2_ADDITIONAL_BROWSER_ARGUMENTS", "--user-agent="+VARIABLE_CONF_UA}
VARIABLE_CONF_STBAR=Get("STBAR"):if VARIABLE_CONF_STBAR="0"{VARIABLE_CONF_STBAR=0}else{VARIABLE_CONF_STBAR=1}
VARIABLE_CONF_PAGECOLOR=Get("DEFPAGECOLOR")
VARIABLE_CONF_WindowColor=Get("WindowColor"):split VARIABLE_CONF_WindowColor,"#",VARIABLE_CONF_WindowColor
VARIABLE_CONF_AUTOSEARCH=int(Get("AUTOSEARCH"))

Set "URLs"
VARIABLE_CONF_URL_FIRST=Get("FIRST")
VARIABLE_CONF_URL_SEARCH=Get("SEARCH")
VARIABLE_CONF_TEXT_ATENE_SEARCH=Get("SEARCH")
VARIABLE_CONF_URL_BROWSERUPDATE=Get("GETUPDATE")
VARIABLE_CONF_URL_TRANSLATE=Get("TRANSLATE")
VARIABLE_CONF_URL_REPORT=Get("REPORT")

Set "Passage"
VARIABLE_CONF_TEXT_SENDBACK=Get("SENDBACK")
VARIABLE_CONF_TEXT_SENDFORWARD=Get("SENDFORWARD")
VARIABLE_CONF_TEXT_SENDRELOAD=Get("SENDRELOAD")
VARIABLE_CONF_TEXT_NAVIGATE=Get("NAVIGATE")
VARIABLE_CONF_TEXT_NEWTAB=Get("NEWTAB")
VARIABLE_CONF_TEXT_CLONETAB=Get("CLONETAB")
VARIABLE_CONF_TEXT_DELETETAB=Get("DELETETAB")
VARIABLE_CONF_TEXT_CAPUTURE=Get("CAPUTURE")
VARIABLE_CONF_TEXT_ISABOUT=Get("ISABOUT")
VARIABLE_CONF_TEXT_LOCAL=Get("LOCAL")
VARIABLE_CONF_TEXT_ATENE_NAME=Get("MANAPADNAME")
VARIABLE_CONF_TEXT_DOWNLOADS=Get("DOWNLOADS")
VARIABLE_CONF_TEXT_CLOSE=Get("CLOSE")
VARIABLE_CONF_TEXT_INICONFIG=Get("INICONFIG")
VARIABLE_CONF_TEXT_ATENE_URL=Get("MOVEURL")
VARIABLE_CONF_TEXT_ATENE_CLOSE=Get("MANACLOSE")
VARIABLE_CONF_TEXT_UPDATE=Get("UPDATE")
VARIABLE_CONF_TEXT_HISTORY=Get("TRACK")
VARIABLE_CONF_TEXT_FUNCTION=Get("FUNCTION1")
VARIABLE_CONF_TEXT_FUNCTION_NAME=Get("FUNCTIONTITLE")
VARIABLE_CONF_TEXT_ATENE_GOURL=Get("MOVEURL")
VARIABLE_CONF_TEXT_ATENE_SEARCH=Get("SEARCHURL")
VARIABLE_CONF_TEXT_SEARCH=Get("SEARCHTEXT")
VARIABLE_CONF_TEXT_TRANSLATE=Get("TRANSLATE")
return

//===============================================================================
*SetColor
	split VARIABLE_CONF_WindowColor(0),",",opt:opt = RGB(opt(0),opt(1),opt(2)):DwmSetWindowAttribute hwnd,DWMWA_CAPTION_COLOR,varptr(opt),4	//色
	split VARIABLE_CONF_WindowColor(1),",",opt:opt = RGB(opt(0),opt(1),opt(2)):DwmSetWindowAttribute hwnd,DWMWA_BORDER_COLOR,varptr(opt),4
	split VARIABLE_CONF_WindowColor(2),",",opt:opt = RGB(opt(0),opt(1),opt(2)):DwmSetWindowAttribute hwnd,DWMWA_TEXT_COLOR,varptr(opt),4
return

*CallExit		//0番以外のxの定義
gsel 0
if wparam!=0{gsel wparam,-1:return}

*ISWindowExit	//終了
title "Please exit from taskmgr"
DestroyMenu hMenu.0
if ( IsWindowVisible(hWnd6) ){SetWindowPos hWnd6, 0, 0, 0, 0, 0, 0x83}

gsel 0,-1
end:end