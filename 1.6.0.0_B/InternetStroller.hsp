#include "hsp3utf.as"

#packopt version "Build.ini"
#packopt icon "image.ico"
#epack "mswv.pak"
#epack "rss.pak"
#epack "html.pak"
#epack "iE.pak"
#epack "iJ.pak"

/*
	□---------------------------□
	 | Internet Stroller Memoria |
	□---------------------------□
	Copyright(C) ABATBeliever 2021-2024
	Website:"https:/abatbeliever.net/InternetStroller/"

	Permission is hereby granted, free of charge, to any person obtaining a copy
	of this software and associated documentation files (the "Software"), to deal
	in the Software without restriction, including without limitation the rights
	to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
	copies of the Software, and to permit persons to whom the Software is
	furnished to do so, subject to the following conditions:
	
	The above copyright notice and this permission notice shall be included in all
	copies or substantial portions of the Software.
	
	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
	IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
	FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
	AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
	LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
	OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
	SOFTWARE.
*/

#define ISName "InternetStroller Memoria"
#define ISDeepVersion "Beta"
#define ISCommonVer "3160"
#define ISBuildNumber "3.160.879.0"
#define ISBuildDay ""+__date__+" "+__time__
ISVersion=ISDeepVersion+" Ver 1.6.0.0B"

screen 0,1000,640,,(ginfo(20)/2-500),(ginfo(21)/2-320) : hBrowser=hwnd : title ISName
redraw 0

//IShwndOpenは1で開いている判定
repeat 7
	IShwndOpen(cnt)=0
loop
/*
0 (メインウィンドウ)
1 RSS
2 IScript
3 ManaPAD
4 Aboutウィンドウ
5 FunctionManager
6 (JavaScript実行)
*/

#include "shell32.as"

#include "src/modTaskDialog.hsp"
#include "src/ini_system.hsp"
#include "src/MultiDialog.hsp"
#include "src/hspinets.as"
#include "src/Notification_Manager.hsp"

#include "src/hspinets.as"

#uselib "gdi32.dll"
#uselib "user32.dll"
#func SetProcessDPIAware "SetProcessDPIAware"

#module
	#deffunc GetInit
		ini_load "ISConfig.ini"
	return

	#defcfunc Get str GetiniSection, str GetiniStr
		sdim tmp , 1024
		ini_section_sel GetiniSection
		ini_get GetiniStr , tmp
		if stat=1 {
			dialog "Invalid value (does not exist or cannot be read)\nNew settings may have been added as a result of a browser update.\nMissing value '"+GetiniStr+"["+GetiniSection+"]'\nEdit(Y)/Ignore(N)\n\n値が無効です\nブラウザが更新された結果、新しい設定が追加された可能性があります\n存在しない値 '"+GetiniStr+"["+GetiniSection+"]'\n編集(Y)/無視(N)",3,"ISConfig Error"
			if stat=6{ShellExecuteW 0, "open", "notepad", dirinfo(1)+"\\ISConfig.ini", 0, 10:end}
		}
	return tmp

	#defcfunc FileCheck str filename
		exist filename
		if strsize=-1{return 1}
	return 0
#global

*INIConf
ini_set
ini_load "ISConfig.ini"
	if stat{
		modTaskSelect=modTasks("Internet Stroller Memoria - No Config","There is no configuration file.\n有効な設定ファイルがありません","Please select the language for the ISConfig.ini.\nISConfig.iniを作るので言語を選択してください。","English\n日本語\nRetry\nExit")
			if modTaskSelect="1" {bcopy "iE.pak",""+dir_cur+"\\ISConfig.ini":dialog "It is set to high DPI mode.\nIf problems arise, you can disable it by setting \"REG_DPI\" to 1 in ini.":goto *ContinueLoad}
			if modTaskSelect="2" {bcopy "iJ.pak",""+dir_cur+"\\ISConfig.ini":dialog "高DPIモードにこの後設定されます。問題が生じた場合はiniの「REG_DPI」を1にすると無効化できます。":goto *ContinueLoad}
			if modTaskSelect="3" {goto *INIConf}
			if modTaskSelect="4" {end}
			goto *INIConf
	}
	
*ContinueLoad
GetInit
if VARIABLE_CONF_DPI=0{SetProcessDPIAware:screen 0,1000,600:title ISName}//DPI
font "FixedSys"

//OSバージョンの把握
 	switch sysinfo(0)
		case "WindowsNT ver5.0":OS="Windows 2000"                   :goto *CompatibleCaution
		case "WindowsNT ver5.1":OS="Windows XP"                     :goto *CompatibleCaution
		case "WindowsNT ver5.2":OS="Windows Server 2003 / ReactOS"  :goto *CompatibleCaution
		case "WindowsNT ver6.0":OS="Windows Vista / Server 2008"    :goto *CompatibleCaution
		case "WindowsNT ver6.1":OS="Windows 7 / Winlator Android"   :goto *CompatibleCaution
		case "WindowsNT ver6.2":OS="Windows 8 / RT"                 :goto *CompatibleCaution
		case "WindowsNT ver6.3":OS="Windows 8.1 / RT"               :goto *CompatibleCaution

		case "WindowsNT ver10.0":OS="Windows 10/11/more"            :goto *ContinueDLL
		
		default : OS="Unknown / Incompatible Windows"               :goto *CompatibleCaution//Error
	swend


*CompatibleCaution
dialog "WebView doesn't support ["+OS+"]\nIgnore(Y)/Safe Exit(N)",2
if stat = "7" {end}

*ContinueDLL
if FileCheck("WebView2Loader.dll")=1{bcopy "mswv.pak",""+dir_cur+"\\WebView2Loader.dll"}

#include "src/LoadWebView.hsp"
gosub *Loadini
#include "src/Graphic.hsp"
#include "src/SetEvent.hsp"

	if dir_cmdline = "" {                    //初期URL。引数指定があればそれを、無ければiniより
		DEF_URL = VARIABLE_CONF_URL_FIRST
		if VARIABLE_CONF_URL_FIRST = "include" { //内蔵が指定された場合はindex.htmlを展開
			exist dirinfo(1) + "\\index.html"
			if strsize = -1 {bcopy "html.pak",""+dir_cur+"\\index.html"+VARIABLE_CONF_PAGECOLOR}
			DEF_URL="file://"+dir_exe+"\\index.html"+VARIABLE_CONF_PAGECOLOR
		}
		gosub *TAB_ADD
	}else{
		DEF_URL = dir_cmdline:gosub *TAB_ADD:DEF_URL="file://"+dir_exe+"\\index.html"
	}
	
	if VARIABLE_CONF_MDI=0{hClient=hWnd:GetWindowLong hClient,-16 :SetWindowLong hClient,-16,stat|$00CF0000}//MDI

	gosub *TAB_UPDATE
	gosub *WM_SIZE
	onerror *ERROR
	redraw 1
	stop

#include "src/WindowEvent.hsp"
#include "src/TabEvent.hsp"
#include "src/WebViewEvent.hsp"
#include "src/UserEvent.hsp"

#include "src/RSSer.hsp"
#include "src/ManaPAD.hsp"
#include "src/Execute6.hsp"
#include "src/IScript.hsp"
#include "src/URLPush.hsp"

*ERROR
SetTrayIconFile "user32.dll"
CreateTrayIcon "",1,0
PopupBalloonTip "Error Report ["+ISVersion+"]",""+ISName+" has Self-Stopped.\n\nError Type>>"+wparam+"\nPlease Report to ABATBeliever",2,0
end

*loadini
VARIABLE_CONF_INIFILEWRITER=Get("HEAD", "WRITER")
VARIABLE_CONF_INIFILEVER=int(Get("HEAD", "VERSION"))
if int(VARIABLE_CONF_INIFILEVER) != 3 {dialog "ISConfig.ini is unknown Version or can't load.\nISConfig.iniが古いバージョンです。削除してください。":end}
VARIABLE_CONF_INIFILELANGUAGE=Get("HEAD", "LANGUAGE")
VARIABLE_CONF_INIFILEBUILD=Get("HEAD", "WRITEDAY")

VARIABLE_CONF_ISCRIPTENABLE=int(Get("REG", "ISCRIPTENABLE"))
VARIABLE_CONF_MDI=int(Get("REG", "MDI"))
VARIABLE_CONF_DPI=int(Get("REG", "DPI")):if VARIABLE_CONF_DPI=0{DPISETPOS=1}else{DPISETPOS=0}
VARIABLE_CONF_UA=Get("REG", "DEFUA"):if VARIABLE_CONF_UA!=""{SetEnvironmentVariable "WEBVIEW2_ADDITIONAL_BROWSER_ARGUMENTS", "--user-agent="+VARIABLE_CONF_UA}
VARIABLE_CONF_STBAR=Get("REG", "STBAR"):if VARIABLE_CONF_STBAR="0"{VARIABLE_CONF_STBAR=0}else{VARIABLE_CONF_STBAR=1}
VARIABLE_CONF_PAGECOLOR=Get("REG", "DEFPAGECOLOR")
VARIABLE_CONF_AUTOSEARCH=int(Get("REG", "AUTOSEARCH"))

VARIABLE_CONF_URL_FIRST=Get("URLs", "FIRST")
VARIABLE_CONF_URL_SEARCH=Get("URLs", "SEARCH")
VARIABLE_CONF_TEXT_ATENE_SEARCH=Get("URLs", "SEARCH")
VARIABLE_CONF_URL_BROWSERUPDATE=Get("URLs", "GETUPDATE")
VARIABLE_CONF_URL_TRANSLATE=Get("URLs", "TRANSLATE")
VARIABLE_CONF_URL_REPORT=Get("URLs", "REPORT")

VARIABLE_CONF_TEXT_SENDBACK=Get("Passage", "SENDBACK")
VARIABLE_CONF_TEXT_SENDFORWARD=Get("Passage", "SENDFORWARD")
VARIABLE_CONF_TEXT_SENDRELOAD=Get("Passage", "SENDRELOAD")
VARIABLE_CONF_TEXT_NAVIGATE=Get("Passage", "NAVIGATE")
VARIABLE_CONF_TEXT_NEWTAB=Get("Passage", "NEWTAB")
VARIABLE_CONF_TEXT_CLONETAB=Get("Passage", "CLONETAB")
VARIABLE_CONF_TEXT_DELETETAB=Get("Passage", "DELETETAB")
VARIABLE_CONF_TEXT_CAPUTURE=Get("Passage", "CAPUTURE")
VARIABLE_CONF_TEXT_ISABOUT=Get("Passage", "ISABOUT")
VARIABLE_CONF_TEXT_LOCAL=Get("Passage", "LOCAL")
VARIABLE_CONF_TEXT_ATENE_NAME=Get("Passage", "MANAPADNAME")
VARIABLE_CONF_TEXT_DOWNLOADS=Get("Passage", "DOWNLOADS")
VARIABLE_CONF_TEXT_CLOSE=Get("Passage", "CLOSE")
VARIABLE_CONF_TEXT_INICONFIG=Get("Passage", "INICONFIG")
VARIABLE_CONF_TEXT_ATENE_URL=Get("Passage", "MOVEURL")
VARIABLE_CONF_TEXT_ATENE_CLOSE=Get("Passage", "MANACLOSE")
VARIABLE_CONF_TEXT_UPDATE=Get("Passage", "UPDATE")
VARIABLE_CONF_TEXT_HISTORY=Get("Passage", "TRACK")
VARIABLE_CONF_TEXT_FUNCTION=Get("Passage", "FUNCTION1")
VARIABLE_CONF_TEXT_FUNCTION_NAME=Get("Passage", "FUNCTIONTITLE")
VARIABLE_CONF_TEXT_ATENE_GOURL=Get("Passage", "MOVEURL")
VARIABLE_CONF_TEXT_ATENE_SEARCH=Get("Passage", "SEARCHURL")
VARIABLE_CONF_TEXT_SEARCH=Get("Passage", "SEARCHTEXT")
VARIABLE_CONF_TEXT_TRANSLATE=Get("Passage", "TRANSLATE")
return

*CallExit
gsel 0
if wparam!=0{gsel wparam,-1:return}

*ISWindowExit
title "Please exit from taskmgr"
DestroyMenu hMenu.0
if ( IsWindowVisible(hWnd6) ){SetWindowPos hWnd6, 0, 0, 0, 0, 0, 0x83}
repeat 5
	if IShwndOpen(cnt+1)=1{gsel cnt,-1}
loop

gsel 0,-1
title "Please exit from taskmgr"
end:end